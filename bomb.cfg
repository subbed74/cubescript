//.bomb replica from sauerstalker by [FD]Venera - Version 1.1
//Uses PMs sent through teamchat (or teamchat in general)

echo "^f6[ MINIGAME ] ^f0Bomb is active"

//Necessary bomb aliases
bomb_colors = [red blue purple green yellow]
bomb_active = 0

//Bomb Commands Container
bomb_control = [
	//Necessary Temp-Aliases
	bomb_command = (at $bomb_sendertext 0)
	
	//COMMAND: .bomb
	if (=s $bomb_command ".bomb") [
		bomb_targetcn = (at $bomb_sendertext 1)
		bomb_targetname = (getclientname $bomb_targetcn)
		
		if (|| (|| (= $bomb_targetcn (getclientnum $bomb_sendercn)) (=s $bomb_targetcn "")) (!= $bomb_active 0)) [
			//If the target is the sender or not specified
			servcmd pm $bomb_sendercn [[ BOMB ] You must specify a valid target, or a bomb is already active!]
		] [
			//Set the bomb
			bomb_answer = (at $bomb_colors (rnd (listlen $bomb_colors)))
			bomb_timer = (rnd 21 6)
			bomb_active = [1 @bomb_targetname @bomb_answer]
			
			//Update the sender/receiver stats
			aliasname = [bombdata_@bomb_sendername]
			if (=s (getalias $aliasname) "") [ alias $aliasname [0 0 0 0] ]
			index_add $aliasname 0
			
			aliasname = [bombdata_@bomb_targetname]
			if (=s (getalias $aliasname) "") [ alias $aliasname [0 0 0 0] ]
			index_add $aliasname 1
			
			//Send the bomb and begin the thing
			servcmd pm $bomb_sendercn [[ BOMB ] You have sent a bomb to @bomb_targetname !]
			servcmd pm $bomb_targetcn [[ BOMB ] @bomb_sendername has sent you a bomb! You have @bomb_timer seconds to .cut the right colored wire!]
			servcmd pm $bomb_targetcn [[ BOMB ] Colors: @bomb_colors]
			echo [[ BOMB ] A bomb was sent to @bomb_targetname by @bomb_sendername]
	
			sleep (* $bomb_timer 1000) [ bomb_detonate $bomb_targetcn ]
		]
	]
	
	//COMMAND: .cut
	if (=s $bomb_command ".cut") [
		//Make sure that the person using cut is the bombed guy and that a bomb is set
		if (&& (= (at $bomb_active 0) 1) (=s (at $bomb_active 1) $bomb_sendername)) [
			//Check for successful cut.  If not, do failure stuff
			if (=s (at $bomb_active 2) (at $bomb_sendertext 1)) [
				bomb_active = 0
				servcmd pm $bomb_sendercn [[ BOMB ] You have successfully defused the bomb!]
				
				//Update stats of target for success
				aliasname = [bombdata_@bomb_sendername]
				index_add $aliasname 2
				
				echo [[ BOMB ] @bomb_sendername managed to defuse the bomb.]
				clearsleep
			] [
				bomb_detonate $bomb_sendercn
			]
		]
	]
	
	//COMMAND: .stats
	if (=s $bomb_command ".bombstats") [
		//Grab player's own stats if own CN or no CN specified
		if (|| (=s (at $bomb_sendertext 1) "") (= (at $bomb_sendertext 1) $bomb_sendercn)) [
			aliasname = [bombdata_@bomb_sendername]
			aliasdata = (getalias $aliasname)
			
			//Make sure the stats exist
			if (=s $aliasdata "") [
				servcmd pm $bomb_sendercn [[ BOMB ] Stats do not yet exist for you.]
			] [
				servcmd pm $bomb_sendercn [[ BOMB ] @bomb_sendername - Sent: @(at $aliasdata 0) | Received: @(at $aliasdata 1) | Defused: @(at $aliasdata 2) | Failed: @(at $aliasdata 3)]
				echo [[ BOMB ] @bomb_sendername has requesed their own stats]
			]
		] [
			//Grab stats of other player
			statname = (getclientname (at $bomb_sendertext 1))
			aliasname = [bombdata_@statname]
			aliasdata = (getalias $aliasname)
			
			//Make sure the stats exist
			if (=s $aliasdata "") [
				servcmd pm $bomb_sendercn [[ BOMB ] Stats do not yet exist for @statname]
			] [
				servcmd pm $bomb_sendercn [[ BOMB ] @statname - Sent: @(at $aliasdata 0) | Received: @(at $aliasdata 1) | Defused: @(at $aliasdata 2) | Failed: @(at $aliasdata 3)]
				echo [[ BOMB ] @bomb_sendername has requested the stats of @statname]
			]
		]
	]
]

//Bomb functions
bomb_detonate = [
	if (= $bomb_active 1) [
		servcmd pm $arg1 [[ BOMB ] *boooooooooooooom*]
		bomb_active = 0
		
		//Update target stats for failure
		aliasname = [bombdata_@(getclientname $arg1)]
		index_add $aliasname 3
		
		echo [[ BOMB ] @(getclientname $arg1) failed to defuse the bomb]
	]
]

//Events
scriptevent "playerteamtext" "bomb_sendercn bomb_sendername bomb_sendertext" $bomb_control

//Necessary Functions
index_add = [
	aliasdata = (getalias $arg1)
	aliasdata_change = (+ (at $aliasdata $arg2) (? (! $arg3) 1 $arg3))
	
	alias $arg1 (listsplice $aliasdata $aliasdata_change $arg2 1)
]
//Cookiebot 1.4 by [FD]Venera
//NOTE: This was made to work with Pastaland's version of handling private messages.  This should work with any server that handles PMs similarly.
//Commands: /allcookies [PMs everyone telling them they have cookies]

//Bot commands: [] = Required; () = Optional
//  .cookie [CN] - Sends a cookie from the sender to the recipient [CN]
//  .stats (CN) - Retrieves the total cookies received and sent of player (CN).  Using no CN requests the stats from the user
//  .ask [CN] - Ask the player [CN] for a cookie.

echo "^f6[ COOKIE ] ^f0Cookiebot Active"

//Switch variables
cookie_connectswitch = 0

//Cookie Commands Container
cookie_control = [
  //Necessary aliases
  playerlist = (listclients 1 1)
  cookie_targetcn = (at $cookie_text 1)
  cookie_targetname = (getclientname $cookie_targetcn)
  
  //COMMAND: .cookie
  if (=s (at $cookie_text 0) ".cookie") [
    if (inlist $playerlist $cookie_targetcn) [
      //Check if player is sending cookie to self
      if (= $cookie_targetcn $cookie_sendercn) [
        servcmd pm $cookie_sendercn [[ COOKIE ] You can't send a cookie to yourself!]
		echo (format "^f6[ COOKIE ] ^f5%1 ^f0tried to send a cookie to themselves." $cookie_sendername)
      ] [
        servcmd pm $cookie_targetcn [[ COOKIE ] You have received a(n) @(at $cookie_text 2) cookie from @cookie_sendername !]
        cookie_stataddreceive $cookie_targetcn
        servcmd pm $cookie_sendercn [[ COOKIE ] Cookie successfully delivered to @cookie_targetname]
        cookie_stataddsent $cookie_sendercn
		echo (format "^f6[ COOKIE ] ^f0A(n) %1 cookie was sent from ^f5%2 ^f0to ^f5%3" (at $cookie_text 2) $cookie_sendername $cookie_targetname)
      ]
    ] [
	  servcmd pm $cookie_sendercn [[ COOKIE ] You must specify a valid CN!]
	  echo (format "^f6[ COOKIE ] ^f5%1 ^f0failed at sending a cookie to a real person." $cookie_sendername)
	]
  ]
  
  //COMMAND: .stats
  if (=s (at $cookie_text 0) ".stats") [
    if (=s $cookie_targetcn "") [
      aliasname = [cookiedata_@cookie_sendername]
      aliasdata = (getalias $aliasname)

      servcmd pm $cookie_sendercn [[ COOKIE STATS ] Received: @(at $aliasdata 0) | Sent: @(at $aliasdata 1)]
	  echo (format "^f6[ COOKIE ] ^f5%1 ^f0requested their own cookie stats." $cookie_sendername)
    ] [
	  if (inlist $playerlist $cookie_targetcn) [
        aliasname = [cookiedata_@cookie_targetname]
        aliasdata = (getalias $aliasname)
		
		servcmd pm $cookie_sendercn [[ COOKIE ] @cookie_targetname | Sent: @(at $aliasdata 1) - Received: @(at $aliasdata 0)]
	    echo (format "^f6[ COOKIE ] ^f5%1 ^f0requested cookie stats for ^f5%2." $cookie_sendername $cookie_targetname)
	  ] [
	    servcmd pm $cookie_sendercn [[ COOKIE ] You must specify a valid CN!]
		echo (format "^f6[ COOKIE ] ^f5%1 ^f0failed at requesting stats." $cookie_sendername)
	  ]
    ]
  ]
  
  //COMMAND: .ask
  if (=s (at $cookie_text 0) ".ask") [
    //Check if asking for cookie from self
    if (|| (= $cookie_sendercn $cookie_targetcn) (! (inlist $playerlist $cookie_targetcn))) [
      servcmd pm $cookie_sendercn [[ COOKIE ] You must specify a valid CN!]
	  echo (format "^f6[ COOKIE ] ^f5%1 ^f0failed at asking for a cookie." $cookie_sendername)
    ] [
      servcmd pm $cookie_targetcn [[ COOKIE ] @cookie_sendername is asking for a cookie!]
      servcmd pm $cookie_sendercn [[ COOKIE ] You are asking for a cookie from @cookie_targetname]
	  echo (format "^f6[ COOKIE ] ^f5%1 ^f0is requesting a cookie from ^f5%2." $cookie_sendername $cookie_targetname)
    ]
  ]
]

//Send cookies to everyone
allcookies = [
  playerlist = (listclients 1 1)
  loop i (listlen $playerlist) [
   servcmd pm (at $playerlist $i) [[ COOKIE ] Here, @(getclientname (at $playerlist $i))! Have a cookie!]
   cookie_stataddreceive @(at $playerlist $i)
  ]
  echo [^f6[ COOKIE ] ^f0Cookies were sent to everyone!]
]

//Send a cookie on connect
cookie_onconnect = [
  if ($cookie_connectswitch) [
    servcmd pm $cookie_connectcn [[ COOKIE ] Hello!  Have a cookie!]
	echo (format "^f6[ COOKIE ] ^f0A cookie was sent to ^f5%1 ^f0for connecting." $cookie_connectname)
  ]
]

//Cookie stat tracking functions
//VARIABLE INFO: cookiedata_NAME = [RECEIVED SENT]
cookie_stataddreceive = [
	aliasname = [cookiedata_@(getclientname $arg1)]
	if (=s (getalias $aliasname) "") [ alias $aliasname [0 0] ]
	index_add $aliasname 0
]

cookie_stataddsent = [
	aliasname = [cookiedata_@(getclientname $arg1)]
	if (=s (getalias $aliasname) "") [ alias $aliasname [0 0] ]
	index_add $aliasname 1
]

//Events
scriptevent "playerconnect" "cookie_connectcn cookie_connectname" $cookie_onconnect
scriptevent "playerteamtext" "cookie_sendercn cookie_sendername cookie_text" $cookie_control
scriptevent "connect" [ sleep 1000 [ cookie_connectswitch = 1 ] ]
scriptevent "disconnect" [ cookie_connectswitch = 0 ]

//Necessary Functions
inlist = [
  if (= (listfind s $arg1 [strcmp $s $arg2]) -1) [ result 0 ] [ result 1 ]
]

index_add = [
	aliasdata = (getalias $arg1)
	aliasdata_change = (+ (at $aliasdata $arg2) (? (! $arg3) 1 $arg3))
	
	alias $arg1 (listsplice $aliasdata $aliasdata_change $arg2 1)
]
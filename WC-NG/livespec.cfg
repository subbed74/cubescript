// Livespec 1.0 by subbed74
// This little script will spectate a player during a game and switch players when the person being followed dies/disconnects.

// Initialize $ls_switch to 0
ls_switch = 0  //- Turns the livespec event on/off

// Loaded script message
echo "^f3[ LIVESPEC ] ^f7Script loaded"

// COMMAND: livespec [CN] - Unspecs if no argument provided
livespec = [
  // Follow the person if arg is present, otherwise unspec
  if ($numargs) [
    follow_cn = $arg1  // Just in case $arg1 disappears

    // If player is not already spectator, spectate
    if (! (isspectator (getclientnum))) [
      spectator 1
      ls_spectator = 0  //- For use when disabling livespec.  It specs/unspecs the player
    ] [
      ls_spectator = 1
    ]

    // Follow the person and flip ls_switch
    sleep 250 [ follow $follow_cn ]
    ls_switch = 1
  ] [
    // Check if livespec is on.  Start livespec on current follow if off
    if ($ls_switch) [
      // Check ls_spectator and un/spec accordingly, turn event off
      spectator (? ($ls_spectator) 1 0)
      ls_switch = 0
    ] [
      ls_switch = 1
    ]
  ]

  // Send a enable/disable message
  echo (format "^f3[ LIVESPEC ] ^f7Livespec has been %1" (? ($ls_switch) "^f0enabled" "^f3disabled"))
]

// Event handlers for livespec
ls_fragevent = [
  if ($ls_switch) [
    if (= $ls_victim_cn (getfollow)) [
      // Follow the next person if the followee suicided (Even though the next person may be dead).  Otherwise, follow the killer
      if (= $ls_killer_cn $ls_victim_cn) [
        nextfollow
      ] [
        follow $ls_killer_cn
      ]
      echo (format "^f3[ LIVESPEC ] ^f7You are now following ^f6%1^f5(%2)" (getclientname (getfollow)) (getfollow))
    ]
  ]
]

ls_discevent = [
  if ($ls_switch) [
    nextfollow
  ]
]

// Install the "playerfrag" scriptevent
scriptevent "playerfrag" "ls_killer_cn null ls_victim_cn null" $ls_fragevent
